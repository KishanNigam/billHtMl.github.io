Dim db, rs, objLOG, objLOGFile, objDATA, objDATAFile
Dim sDate, RPTDATE, YYEAR, monthno, dayno, hrno, minno
Dim LOGFilePath, DATAFilePath, sConnect, sQuery
Dim NUMCKS, AMTCKS, TOTCKS, TOTAMT

' Initialize date-related variables
sDate = Year(Now) 
YYEAR = Year(Now)  
monthno = ZeroFill(Month(Now), 2)
dayno = ZeroFill(Day(Now), 2)
hrno = ZeroFill(Hour(Now), 2)
minno = ZeroFill(Minute(Now), 2)

sDate = YYEAR & monthno & dayno & hrno & minno
RPTDATE = YYEAR & "-" & monthno & "-" & dayno

NUMCKS = 0
AMTCKS = 0
TOTCKS = 0
TOTAMT = 0

' Set up the log file
LOGFilePath = "\\bhsieas11\server\custom\log\SB-TestV01\OFAC_VBS_" & sDate & ".LOG"
Set objLOG = CreateObject("Scripting.FileSystemObject")
Set objLOGFile = objLOG.CreateTextFile(LOGFilePath, True)
objLOGFile.WriteLine("OFAC EXPORT REPORT      RUN DATE: " & Date & "  " & Time)
objLOGFile.WriteLine("   ")

' Set up the data file
DATAFilePath = "\\bhsieas11\server\custom\log\SB-TestV01\BHSIEAS_Intl_" & sDate & ".TXT"
Set objDATA = CreateObject("Scripting.FileSystemObject")
Set objDATAFile = objDATA.CreateTextFile(DATAFilePath, True)
objLOGFile.WriteLine("File Created")

' Establish the database connection
sConnect = "DRIVER={SQL Server};SERVER=NICOSQL108\BHSIEAS_TEST;UID=SYSTEM;Trusted_Connection=yes;DATABASE=EAS;"
Set db = CreateObject("ADODB.Connection")
db.CommandTimeout = 0
db.Open sConnect 

objLOGFile.WriteLine("Connected")

' Execute the query
sQuery = "SELECT DISTINCT C.TIN AS DATA_FIELD FROM AP_BSTRN_HEADER C WHERE C.TRAN_ID IN (121092,121093)"

Set rs = db.Execute(sQuery)

' Process the results
If Not rs.EOF Then
    Do Until rs.EOF 
        objDATAFile.WriteLine(rs.Fields("DATA_FIELD").Value)
        NUMCKS = NUMCKS + 1
        rs.MoveNext
    Loop
End If

' Close recordset and database connection
rs.Close
Set rs = Nothing
db.Close
Set db = Nothing

' Log the number of checks
objLOGFile.WriteLine("NUMBER OF CHECKS = " & NUMCKS)
objLOGFile.WriteLine("  ")
objLOGFile.WriteLine("OFAC EXPORT ENDED SUCCESSFULLY")

' Close the files
objDATAFile.Close
Set objDATAFile = Nothing
objLOGFile.Close
Set objLOGFile = Nothing

' Clean up objects
Set objDATA = Nothing
Set objLOG = Nothing

WScript.Quit(0)

' Function to read registry values
Function ReadReg(RegPath)
    Dim objRegistry, Key
    Set objRegistry = CreateObject("Wscript.shell")
    Key = objRegistry.RegRead(RegPath)
    ReadReg = Key
    Set objRegistry = Nothing
End Function

' Function to pad numbers with leading zeros
Function ZeroFill(ByVal String, ByVal length)
    ZeroFill = Trim(String)
    Do While Len(ZeroFill) < length
        ZeroFill = "0" & ZeroFill
    Loop
End Function
