ALTER PROCEDURE [NICO].[TrialBalance_2Basis]
    @PRIOR_PERIOD VARCHAR(10),
    @CURRENT_PERIOD VARCHAR(10),
    @BASIS1 VARCHAR(50),
    @BASIS2 VARCHAR(50),	
    @PRIOR_PERIOD2 VARCHAR(10),
    @COMPANY_CODE VARCHAR(50),
	@BASIS3 VARCHAR(50) =NULL 
AS
BEGIN
    DECLARE @sql NVARCHAR(MAX)
    DECLARE @database NVARCHAR(100)

    SET @database = (
        SELECT name 
        FROM sys.databases
        WHERE name LIKE 'EAS_REPL' OR name LIKE 'REPL_EAS'
    )

    SET @sql = '
    SELECT
        BD.CAL_ACCTG_PERIOD,
        BD.COMPANY_CODE,
        BD.ACCOUNT_NUMBER,
        AC.DESCRIPTION,
        SUM(BD.CONVERTED_AMOUNT) AS BALANCE,
        BH.STATUS_CODE,
        A.ACCTG_BASIS_CODE
    FROM 
       ' + QUOTENAME(@database) + '.[dbo].BSTRN_HEADER BH
    INNER JOIN
       ' + QUOTENAME(@database) + '.[dbo].BSTRN_DETAIL BD ON BH.TRAN_ID = BD.TRAN_ID
    INNER JOIN
       ' + QUOTENAME(@database) + '.[dbo].ACCT_BAS_ACCT_BAS A ON BD.ACCTG_BASIS_CODE = A.REL_ACCT_BAS_CODE
    FULL OUTER JOIN
       ' + QUOTENAME(@database) + '.[dbo].ACCOUNT AC ON BD.ACCOUNT_NUMBER = AC.ACCT_NUMBER AND BD.COMPANY_CODE = AC.COMPANY_CODE
    WHERE
       (
        (BD.CAL_ACCTG_PERIOD >= @PRIOR_PERIOD AND BD.CAL_ACCTG_PERIOD <= @CURRENT_PERIOD)
         AND   (((@BASIS3 is NULL) AND A.ACCTG_BASIS_CODE IN (@BASIS1, @BASIS2)) OR ((@BASIS3 is not NULL) and A.ACCTG_BASIS_CODE IN (@BASIS1, @BASIS2,@BASIS3)))
        AND BD.COMPANY_CODE = @COMPANY_CODE  
        AND AC.ACCT_CLASS_IND IN (''A'', ''L'', ''S'', ''E'', ''G'')
       )
       OR
        (
        (BD.CAL_ACCTG_PERIOD >= @PRIOR_PERIOD2 AND BD.CAL_ACCTG_PERIOD <= @CURRENT_PERIOD)
        AND   (((@BASIS3 is NULL) AND A.ACCTG_BASIS_CODE IN (@BASIS1, @BASIS2)) OR ((@BASIS3 is not NULL) and A.ACCTG_BASIS_CODE IN (@BASIS1, @BASIS2,@BASIS3)))
        AND BD.COMPANY_CODE = @COMPANY_CODE  
        AND AC.ACCT_CLASS_IND IN (''I'', ''X'', ''F'')
       )
    GROUP BY
        BD.CAL_ACCTG_PERIOD,
        BD.COMPANY_CODE,
        BD.ACCOUNT_NUMBER,
        AC.DESCRIPTION,
        BH.STATUS_CODE,
        A.ACCTG_BASIS_CODE
    ORDER BY
        BD.ACCOUNT_NUMBER,
        BD.CAL_ACCTG_PERIOD;'

    EXEC sp_executesql @sql, 
        N'@PRIOR_PERIOD VARCHAR(10), @CURRENT_PERIOD VARCHAR(10), @BASIS1 VARCHAR(50), @BASIS2 VARCHAR(50), @BASIS3 VARCHAR(50), @PRIOR_PERIOD2 VARCHAR(10), @COMPANY_CODE VARCHAR(50)',
        @PRIOR_PERIOD, @CURRENT_PERIOD, @BASIS1, @BASIS2, @BASIS3, @PRIOR_PERIOD2, @COMPANY_CODE
END
