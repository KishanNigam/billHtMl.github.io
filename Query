Dim db, rs
Dim sDate, RPTDATE, YYEAR, mmno, monthno, ddno, dayno, hrs, mns, hrno, minno
Dim LOGFilePath, DATAFilePath, objLOG, objLOGFile, objDATA, objDATAFile

' Construct Date-Time Stamp for File Naming
sDate = Year(Now)
YYEAR = Year(Now)  
mmno = Month(Now)
monthno = ZeroFill(mmno, 2)
sDate = sDate & monthno   
RPTDATE = YYEAR & "-" & monthno
ddno = Day(Now)
dayno = ZeroFill(ddno, 2)
sDate = sDate & dayno  
RPTDATE = RPTDATE & "-" & dayno
hrs = Hour(Now)
mns = Minute(Now)
hrno = ZeroFill(hrs, 2)
minno = ZeroFill(mns, 2)
sDate = sDate & hrno & minno

' Define File Paths
LOGFilePath = "C:\Backup\VNDLY_EXPORTS_VBS_" & sDate & ".LOG"
DATAFilePath = "C:\Backup\vt_9f3808fd_1269_423c_a745_879be1f28416_inv_payments_" & sDate & ".CSV"

' Create Log File
Set objLOG = CreateObject("Scripting.FileSystemObject")
Set objLOGFile = objLOG.CreateTextfile(LOGFilePath, True)
objLOGFile.WriteLine("VNDLY EXPORTS REPORT      RUN DATE: " & DATE & "  " & TIME)
objLOGFile.WriteLine("   ")

' Create CSV File
Set objDATA = CreateObject("Scripting.FileSystemObject")
Set objDATAFile = objDATA.CreateTextfile(DATAFilePath, True)

' Write CSV Header Row
objDATAFile.WriteLine("invoice_number,voucher_no,vendor_id,vendor_name,payment_name,payment_number,payment_date,paid_amount,discount_taken_amount,late_charge_amount,notes,payment_method,vendor_invoice_number,payment_id")

' Database Connection
sConnect = "DRIVER={ODBC Driver 17 for SQL Server};SERVER=NICOSQL88\NICOEAS_PROD;UID=SYSTEM;Trusted_Connection=yes;DATABASE=eas;"
Set db = CreateObject("ADODB.Connection")
db.CommandTimeout = 0
db.Open sConnect 

objLOGFile.WriteLine("Database Connection Established.")

' SQL Query
sQuery = "SELECT DISTINCT " & _
         " HDR.REFERENCE_DATA AS invoice_number, " & _
         " '' AS voucher_no, " & _
         " RIGHT(HDR.REFERENCE_DATA, LEN(HDR.REFERENCE_DATA) - 2) AS vendor_id, " & _
         " '' AS vendor_name, " & _
         " '' AS payment_name, " & _
         " APSTAT.CHECK_NUMBER AS payment_number, " & _
         " APHDR.CHECK_DATE AS payment_date, " & _
         " APHDR.CHECK_AMT AS paid_amount, " & _
         " '' AS discount_taken_amount, " & _
         " '' AS late_charge_amount, " & _
         " '' AS notes, " & _
         " APHDR.PAYMENT_MODE AS payment_method, " & _
         " '' AS vendor_invoice_number, " & _
         " '' AS payment_id " & _
         " FROM [eas].[dbo].[AP_CHECK_STATUS] APSTAT " & _
         " JOIN (SELECT [TRAN_ID], " & _
         "            CASE WHEN [CHECK_NUMBER]='' THEN EFT_ADVICE_NUM ELSE CHECK_NUMBER END AS CHKNO, " & _
         "            [COMPANY_CODE], [CHECK_RECON_DATE], [CHECK_DATE], [CHECK_AMT], " & _
         "            [PAYMENT_MODE], [BANK], [BANK_ACCOUNT], [VENDOR_NAME1] " & _
         "       FROM [eas].[dbo].[AP_BSTRN_HEADER] " & _
         "       WHERE CHECK_RECON_STATUS IN ('O','C','V')) APHDR " & _
         "  ON APHDR.COMPANY_CODE=APSTAT.COMPANY_CODE " & _
         "  AND APHDR.BANK=APSTAT.BANK " & _
         "  AND APHDR.BANK_ACCOUNT=APSTAT.BANK_ACCOUNT " & _
         "  AND APHDR.CHKNO = APSTAT.CHECK_NUMBER " & _
         " JOIN BSTRN_HEADER HDR " & _
         "  ON HDR.TRAN_ID = APHDR.TRAN_ID " & _
         "  AND HDR.SOURCE_CODE IN ('APRRG') " & _
         " WHERE APHDR.COMPANY_CODE = 'NICO' " & _
         " AND APHDR.PAYMENT_MODE = 'E' " & _
         " AND APHDR.VENDOR_NAME1 <> '' " & _
         " AND HDR.REFERENCE_DATA LIKE 'INV%'"

Set rs = db.execute(sQuery)

objLOGFile.WriteLine("Query Executed.")

' Write Data to CSV
On Error Resume Next
rs.MoveFirst
Do Until rs.EOF
    objDATAFile.WriteLine(rs.Fields("invoice_number") & "," & _
                          rs.Fields("voucher_no") & "," & _
                          rs.Fields("vendor_id") & "," & _
                          rs.Fields("vendor_name") & "," & _
                          rs.Fields("payment_name") & "," & _
                          rs.Fields("payment_number") & "," & _
                          rs.Fields("payment_date") & "," & _
                          rs.Fields("paid_amount") & "," & _
                          rs.Fields("discount_taken_amount") & "," & _
                          rs.Fields("late_charge_amount") & "," & _
                          rs.Fields("notes") & "," & _
                          rs.Fields("payment_method") & "," & _
                          rs.Fields("vendor_invoice_number") & "," & _
                          rs.Fields("payment_id"))
    rs.MoveNext
Loop

objLOGFile.WriteLine("Data Written to CSV Successfully.")

' Close Database Connection
rs.Close
db.Close
Set rs = Nothing
Set db = Nothing

' Close Files
objDATAFile.Close
objLOGFile.Close

WScript.Echo "Export Completed Successfully."
WScript.Quit(0)

' Helper Functions
Function ZeroFill(ByVal num, ByVal length)
    ZeroFill = Trim(num)
    Do While Len(ZeroFill) < length
        ZeroFill = "0" & ZeroFill
    Loop
End Function
