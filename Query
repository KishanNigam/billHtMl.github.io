echo Started RUN_ MultiCheckSS.bat 
@ECHO OFF

REM Assign Rearrange Format Parameter.
SET environment=%1
echo The environment is [%environment%].
SET server=%2
echo The server is [%server%].
SET RRNGFMT=APSS
echo Rearrange format is [%RRNGFMT%].
SET sourcedir=%3
echo The source directory is [%sourcedir%].
SET RRNGDIR=%5
echo The rearrange directory is [%RRNGDIR%].
SET RRNGFILE=%6
echo The rearrange file name is [%RRNGFILE%].
echo .

REM Date Variable Definition
FOR /F "TOKENS=1* DELIMS= " %%A IN ('DATE/T') DO SET CDATE=%%B 
For /f "tokens=2-4 delims=/ " %%a in ('date /t') do (set date=%%a%%b%%c)

set year=%date:~4,4%
set month=%date:~0,2%
set yearmonth=%year%%month%
SET day=%date:~2,2%
echo day=%day%
SET hour=%time:~0,2%
echo hour=%hour%
IF "%hour:~0,1%" == " " SET hour=0%hour:~1,1%
echo hour=%hour%
SET minute=%time:~3,2%
SET filetimestamp=%year%%month%%day%_%hour%%minute%
echo filetimestamp=%filetimestamp%

if %environment%==PROD (
    SET sourcedir="\\nicoeas10\EAS\SGImports\MULTICHECKUPLOAD-SS\"
    SET archivedir="\\nicoad\nico\Finance\EAS\Archive\SGImports\MULTICHECKUPLOAD-SS"	 
)
if %environment%==TEST (
    SET sourcedir="\\nicoeas12\EAS\SGImports\MULTICHECKUPLOAD-SS\"
    SET archivedir="\\nicoad\nico\Finance\EASTest\Archive\SGImports\MULTICHECKUPLOAD-SS"
)
if %environment%==UAT (
    SET sourcedir="\\nicoeas14\EAS\SGImports\MULTICHECKUPLOAD-SS\"
    SET archivedir="\\nicoad\nico\Finance\EASUAT\Archive\SGImports\MULTICHECKUPLOAD-SS"
)

REM DECLARE VARIABLES
SET logdir="\\%server%\server\custom\log\MultiCheckSS"

REM Select only the first matching source file.
FOR %%F IN (%sourcedir%*_MultiCheckUpload_SS_*.dat) DO (
    SET sourcefile=%%F
    GOTO ProcessFile
)

:ProcessFile
echo Processing file: %sourcefile%

REM Create the log directory if it does not exist.
SET LogDir=%logdir%\%year%
IF NOT EXIST %LogDir%\NUL MKDIR %LogDir%
SET LogDir=%LogDir%\%yearmonth%
IF NOT EXIST %LogDir%\NUL MKDIR %LogDir%
SET logfilename=%LogDir%\MultiCheck_SS_%filetimestamp%.log
echo The logfilename is [%logfilename%].

echo +++++++++++++++++++++++++++++++++++++++++ >>%logfilename%
echo .                                         >>%logfilename%
echo Start time %date% %time%                  >>%logfilename%
echo User [%username%]                         >>%logfilename%
echo .                                         >>%logfilename%
echo environment = [%environment%].            >>%logfilename%
echo server = [%server%].                      >>%logfilename%
echo archivedir = [%archivedir%].              >>%logfilename%
echo logdir = [%logdir%].                      >>%logfilename%
echo sourcedir = [%sourcedir%].                >>%logfilename%
echo sourcefile = [%sourcefile%].              >>%logfilename%
echo RRNGFMT = [%RRNGFMT%].                    >>%logfilename%
echo .                                         >>%logfilename%

REM Check if the source file exists.
echo Checking to see if [%sourcefile%] exists.          >>%logfilename%
IF NOT EXIST %sourcefile% (
   echo The [%sourcefile%] source file does not exist.  >>%logfilename%
   echo THE [%sourcefile%] SOURCE FILE DOES NOT EXIST!!!
   echo .                                               >>%logfilename%
   GOTO END
)

REM Archive the source file.
echo Archiving the [%sourcefile%] file.                                                      >>%logfilename%
SET archivedir=%archivedir%\%year%
IF NOT EXIST %archivedir%\NUL MKDIR %archivedir%                                             >>%logfilename%
SET archivedir=%archivedir%\%yearmonth%
IF NOT EXIST %archivedir%\NUL MKDIR %archivedir%                                             >>%logfilename%
echo Executing [COPY %sourcefile% %archivedir%].                                             >>%logfilename%
COPY %sourcefile% %archivedir%                                                               >>%logfilename%
echo Finished archiving the [%sourcefile%].                                                  >>%logfilename%
echo .                                                                                       >>%logfilename%

REM Move the source file to the rearrange directory.
echo Moving the [%sourcefile%] to [%RRNGDIR%].                                               >>%logfilename%
MOVE %sourcefile% %RRNGDIR%                                                                  >>%logfilename%
echo Finished moving the [%sourcefile%] to [%RRNGDIR%].                                      >>%logfilename%
echo .                                                                                       >>%logfilename%

REM Prep the rearrange file.
echo Prepping the [%RRNGDIR%%sourcefile%] file for import.                                   >>%logfilename%
echo Renaming the [%RRNGDIR%%sourcefile%] to [%RRNGFILE%].                                   >>%logfilename%
REN %RRNGDIR%%sourcefile% %RRNGFILE%                                                         >>%logfilename%
echo Finished renaming [%RRNGDIR%%sourcefile%] to [%RRNGFILE%].                              >>%logfilename%
echo .                                                                                       >>%logfilename%

REM Start Rearrange
echo Starting Rearrange for [%RRNGFILE%].                                                    >>%logfilename%
pushd D:\eas\server\bin
call RRNG -AP
echo Finished Rearrange for [%RRNGFILE%].                                                    >>%logfilename%
echo .                                                                                       >>%logfilename%

REM Start Validation
echo Starting Validation for [%RRNGFILE%].                                                   >>%logfilename%
CALL VALDAP.EXE
echo Finished Validation for [%RRNGFILE%].                                                   >>%logfilename%
echo .                                                                                       >>%logfilename%

:END
echo End time %date% %time%                        >>%logfilename%
echo +++++++++++++++++++++++++++++++++++++++++     >>%logfilename%
echo .                                             >>%logfilename%
echo Finished RUN_ MultiCheckSS.bat                      >>%logfilename%
